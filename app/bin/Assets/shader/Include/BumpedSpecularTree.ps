precision mediump float;

UniformTexture(binding = 2) uniform sampler2D _MainTex;
UniformTexture(binding = 4) uniform sampler2D _Lightmap;
UniformTexture(binding = 5) uniform sampler2D _Normal;
UniformTexture(binding = 6) uniform sampler2D _SpecMap;
UniformTexture(binding = 7) uniform sampler2D _NHxRoughness;

UniformBuffer(std140, binding = 3) uniform buf_ps {
	vec4 _Color;
	vec4 _Spec;
	vec4 _Smoothness;
	vec4 _WorldSpaceLightPos;
	vec4 _LightColor;
	vec4 _WorldSpaceCameraPos;
	vec4 _CutAlpha;
} u_buf;

Varying(location = 0) in vec2 v_uv;
Varying(location = 1) in vec2 v_uv2;
Varying(location = 2) in vec4 v_t2w0;
Varying(location = 3) in vec4 v_t2w1;
Varying(location = 4) in vec4 v_t2w2;

layout (location = 0) out vec4 o_frag;

vec2 pow4(vec2 x) { return x * x * x * x; }

void main() {
	vec4 c = texture(_MainTex, v_uv) * u_buf._Color;
	if(c.a < u_buf._CutAlpha.x)
	{
		discard;
	}
	
	vec3 n = texture(_Normal, v_uv).rgb * 2.0 - 1.0;
	vec4 lm = texture(_Lightmap, v_uv2) * 2.0;
	vec4 spec_map = texture(_SpecMap, v_uv);

	vec3 pos = vec3(v_t2w0.w, v_t2w1.w, v_t2w2.w);
	vec3 normal = normalize(vec3(dot(v_t2w0.xyz, n), dot(v_t2w1.xyz, n), dot(v_t2w2.xyz, n)));
	vec3 light_dir = normalize(u_buf._WorldSpaceLightPos.rgb - pos * u_buf._WorldSpaceLightPos.w);
	vec3 view_dir = normalize(u_buf._WorldSpaceCameraPos.rgb - pos);
	vec3 light_color = u_buf._LightColor.rgb;
	
	float nl = max(dot(normal, light_dir), 0.0);
	vec3 h = normalize(light_dir + view_dir);
	float nh = max(dot(normal, h), 0.0);
	float nv = max(dot(normal, view_dir), 0.0);
	vec3 ref = reflect(view_dir, normal);
	
	vec3 spec_color = u_buf._Spec.rgb * spec_map.rgb;
	float smoothness = u_buf._Smoothness.x * spec_map.a;
	float rlp4 = pow4(vec2(dot(ref, light_dir), 1.0 - nv)).x;
	float spec = texture(_NHxRoughness, vec2(rlp4, smoothness)).r * 16.0;
	
	vec3 lightmap = c.rgb * lm.rgb;
	vec3 diffuse = c.rgb * light_color * nl;
	vec3 specular = light_color * spec * spec_color;
	
	c.rgb = lightmap + diffuse + specular;

	o_frag = c;
}